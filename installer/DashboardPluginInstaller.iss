; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "ClearDashboardWebApiPlugin"
#define MyAppVersion "1.5"
#define MyAppPublisher "Clear Bible, Inc."
#define MyAppURL "https://www.clear.bible/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{9D36B9BE-15E8-4321-AD93-1C4D70491A94}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\Program Files\Paratext 9\plugins\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=Output
OutputBaseFilename=ClearDashboardPlugin
SetupIconFile=..\src\ClearDashboard.WPF\Assets\ClearDashboard_logo.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern
DisableDirPage=yes
ArchitecturesAllowed=x64
Uninstallable=no
;UninstallDisplayIcon={uninstallexe}
;UninstallIconFile=..\src\ClearDashboard.WPF\Assets\ClearDashboard_logo.ico
;UninstallFilesDir={win}\Installer

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "..\src\ClearDashboard.WebApiParatextPlugin\bin\Debug\net48\*"; DestDir: "{code:GetParatextInstallationPath}\plugins\{#MyAppName}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\src\ClearDashboard.WebApiParatextPlugin\bin\Debug\net48\ClearDashboard.WebApiParatextPlugin.dll"; DestDir: "{code:GetParatextInstallationPath}\plugins\{#MyAppName}"; DestName: "ClearDashboard.WebApiParatextPlugin.ptxplg"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]
var
  ParatextInstallationPath: string;

function InitializeSetup(): Boolean;
var
  Value: string;
begin
  if RegQueryStringValue(
       HKEY_LOCAL_MACHINE, 'SOFTWARE\WOW6432Node\Paratext\8',
       'Paratext9_Full_Release_AppPath', Value) then
    begin
      ParatextInstallationPath := ExtractFileDir(Value);
      Log(Format('APPLICATION installed to %s', [ParatextInstallationPath]));
      Result := True;
    end
  else
    begin
      MsgBox('Paratext is either missing or not up to date.  You must have Paratext to use this plugin.', mbError, MB_OK);
      Result := False;
    end;
end;

function GetParatextInstallationPath(Param: string): string;
begin
  Result := ParatextInstallationPath;
end;